%%% Classical linear logic
%%% Author: Frank Pfenning


% Formula example
M : o
 = (forall [x:i] A x with perp (B x)).

% Correct
D1 : {A:o} {B:o}
         neg (A times B)
      -o pos (B times A)
      -o #
 = ([A:o] [B:o]
      [n^neg (A times B)]
      [p^pos (B times A)]
      timesl ^ ([n1^neg A] [n2^neg B]
		timesr ^ ([p2^pos B] axiom ^ n2 ^ p2)
	               ^ ([p1^pos A] axiom ^ n1 ^ p1)
	               ^ p)
             ^ n).

% An ambiguous proof term
% There are four proof that this is linear, corresponding
% to the four distributions A and B to the left and right,
% but only one proof-term: it can be checked in 4 ways.

D2 : {A:o} {B:o}
         neg (A times B)
      -o pos (top times top)
      -o #
 = ([A:o] [B:o]
      [n^neg (A times B)]
      [p^pos (top times top)]
      timesl ^ ([n1^neg A] [n2^neg B]
		timesr ^ ([p1^pos top] topr ^ () ^ p1)
		       ^ ([p2^pos top] topr ^ () ^ p2)
		       ^ p)
             ^ n).

%  A with B --> B with A
D3 : {A:o} {B:o}
         neg (A with B)
      -o pos (B with A)
      -o #
 = ([A:o] [B:o]
      [n^neg (A with B)]
      [p^pos (B with A)]
      withr ^ (([p2^pos B] withl2 ^ ([n2^neg B] axiom ^ n2 ^ p2) ^ n),
	       ([p1^pos A] withl1 ^ ([n1^neg A] axiom ^ n1 ^ p1) ^ n))
            ^ p).

% !(A -o B)  -o (! A -o ! B)

D4 : {A:o} {B:o}
      pos (perp ((! (perp (A times (perp B))))
		 times (((! A) times (perp (! B))))))
      -o #
 = ([A:o] [B:o]
      [p^pos (perp ((! (perp (A times (perp B))))
		    times ((! A) times (perp (! B)))))]
      perpr ^ ([n1^neg ((! (perp (A times (perp B))))
			times ((! A) times (perp (! B))))]
	       timesl ^ ([n2^neg (! (perp (A times (perp B))))]
			 [n3^neg ((! A) times (perp (! B)))]
			 timesl ^ ([n4^neg (! A)]
				   [n5^neg (perp (! B))]
				   perpl ^ ([p6^pos (! B)]
					    !l ^ ([n7!:neg! (perp (A times (perp B)))]
						  !l ^ ([n8!:neg! A]
						        !r ([p9^pos B] 
							    !d ^ ([n10^neg A]
								  !d ^ ([n11^neg (perp (A times (perp B)))]
									perpl ^ ([p12^pos (A times (perp B))]
										 timesr ^ ([p13^pos A] axiom ^ n10 ^ p13)
										        ^ ([p14^pos (perp B)]
											   perpr ^ ([n15^neg B] axiom ^ n15 ^ p9)
											         ^ p14)
										        ^ p12)
									      ^ n11)
								        n7!)
							         n8!)
						         ^ p6)
						     ^ n4)
					       ^ n2)
				         ^ n5)
			        ^ n3)
		      ^ n1)
            ^ p).


% Example of admissibility from Appendix

% !(A -o B) --> ! A -o ! B

D5 : {A:o} {B:o}
         neg (! (perp (A times (perp B))))
      -o pos (perp ((! A) times (perp (! B))))
      -o #
 = ([A:o] [B:o]
      [n^neg (! (perp (A times (perp B))))]
      [p^pos (perp ((! A) times (perp (! B))))]
      perpr ^ ([n1^neg ((! A) times (perp (! B)))]
	       timesl ^ ([n4^neg (! A)]
			 [n5^neg (perp (! B))]
			 perpl ^ ([p6^pos (! B)]
				  !l ^ ([n7!:neg! (perp (A times (perp B)))]
				        !l ^ ([n8!:neg! A]
					      !r ([p9^pos B] 
						  !d ^ ([n10^neg A]
							!d ^ ([n11^neg (perp (A times (perp B)))]
							      perpl ^ ([p12^pos (A times (perp B))]
								       timesr ^ ([p13^pos A] axiom ^ n10 ^ p13)
								              ^ ([p14^pos (perp B)]
										 perpr ^ ([n15^neg B] axiom ^ n15 ^ p9)
										       ^ p14)
									      ^ p12)
								    ^ n11)
							     n7!)
						       n8!)
					       ^ p6)
				           ^ n4)
				     ^ n)
			       ^ n5)
		      ^ n1)
            ^ p).

% ! A -o ! B --> ? (B ^) -o ? (A ^)

D6 : {A:o} {B:o}
         neg (perp ((! A) times (perp (! B))))
      -o pos (perp ((? (perp B)) times (perp (? (perp A)))))
      -o #
 = ([A:o] [B:o]
      [n^neg (perp ((! A) times (perp (! B))))]
      [p^pos (perp ((? (perp B)) times (perp (? (perp A)))))]
      perpr ^ ([n1^neg ((? (perp B)) times (perp (? (perp A))))]
	       perpl ^ ([p2^pos ((! A) times (perp (! B)))]
			timesl ^ ([n3^neg (? (perp B))]
				  [n4^neg (perp (? (perp A)))]
				  timesr ^ ([p5^pos (! A)] 
					    perpl ^ ([p6^pos (? (perp A))]
						     ?r ^ ([p7?:pos? (perp A)]
							   !r ([p8^pos A]
								 ?d ^ ([p9^pos (perp A)]
								       perpr ^ ([n10^neg A] axiom ^ n10 ^ p8)
									     ^ p9)
								      p7?)
							    ^ p5)
						        ^ p6)
					          ^ n4)
				         ^ ([p11^pos (perp (! B))]
					    perpr ^ ([n12^neg (! B)]
						     !l ^ ([n13!:neg! B]
							   ?l ([n14^neg (perp B)]
							       !d ^ ([n15^neg B]
								     perpl ^ ([p16^pos B] axiom ^ n15 ^ p16)
								           ^ n14)
								    n13!)
							    ^ n3)
						        ^ n12)
					          ^ p11)
				         ^ p2)
			       ^ n1)
		     ^ n)
            ^ p).


% Admissibility applied to previous two derivations.

cf : o = perp ((! A) times (perp (! B))).

%query 1 1

{A:o} {B:o}
{N:neg (! (perp (A times (perp B))))}
{P:pos (perp ((? (perp B)) times (perp (? (perp A)))))}
  ad cf (D5 A B ^ N) ([n^neg cf] D6 A B ^ n ^ P) (D7 A B ^ N ^ P).



% Example of admissibility from Appendix (in lv-cut)

% !(A -o B) --> ! A -o ! B


D5+ : {A:o} {B:o}
          neg (! (perp (A times (perp B))))
       -o pos (perp ((! A) times (perp (! B))))
       -o #+
 = ([A:o] [B:o]
      [n^neg (! (perp (A times (perp B))))]
      [p^pos (perp ((! A) times (perp (! B))))]
      perpr+ ^ ([n1^neg ((! A) times (perp (! B)))]
	        timesl+ ^ ([n4^neg (! A)]
			   [n5^neg (perp (! B))]
			   perpl+ ^ ([p6^pos (! B)]
				     !l+ ^ ([n7!:neg! (perp (A times (perp B)))]
				            !l+ ^ ([n8!:neg! A]
					           !r+ ([p9^pos B] 
						        !d+ ^ ([n10^neg A]
							       !d+ ^ ([n11^neg (perp (A times (perp B)))]
							              perpl+ ^ ([p12^pos (A times (perp B))]
								                timesr+ ^ ([p13^pos A] axiom+ ^ n10 ^ p13)
								                        ^ ([p14^pos (perp B)]
										           perpr+ ^ ([n15^neg B] axiom+ ^ n15 ^ p9)
										                  ^ p14)
									                ^ p12)
								            ^ n11)
							              n7!)
						              n8!)
					             ^ p6)
				                ^ n4)
				         ^ n)
			          ^ n5)
		        ^ n1)
             ^ p).

% ! A -o ! B --> ? (B ^) -o ? (A ^)

D6+ : {A:o} {B:o}
          neg (perp ((! A) times (perp (! B))))
       -o pos (perp ((? (perp B)) times (perp (? (perp A)))))
       -o #+
 = ([A:o] [B:o]
      [n^neg (perp ((! A) times (perp (! B))))]
      [p^pos (perp ((? (perp B)) times (perp (? (perp A)))))]
      perpr+ ^ ([n1^neg ((? (perp B)) times (perp (? (perp A))))]
	        perpl+ ^ ([p2^pos ((! A) times (perp (! B)))]
			  timesl+ ^ ([n3^neg (? (perp B))]
				     [n4^neg (perp (? (perp A)))]
				     timesr+ ^ ([p5^pos (! A)] 
					        perpl+ ^ ([p6^pos (? (perp A))]
						          ?r+ ^ ([p7?:pos? (perp A)]
							         !r+ ([p8^pos A]
								      ?d+ ^ ([p9^pos (perp A)]
								             perpr+ ^ ([n10^neg A] axiom+ ^ n10 ^ p8)
									            ^ p9)
								            p7?)
							           ^ p5)
						              ^ p6)
					               ^ n4)
				             ^ ([p11^pos (perp (! B))]
					        perpr+ ^ ([n12^neg (! B)]
						          !l+ ^ ([n13!:neg! B]
							         ?l+ ([n14^neg (perp B)]
							              !d+ ^ ([n15^neg B]
								             perpl+ ^ ([p16^pos B] axiom+ ^ n15 ^ p16)
								                    ^ n14)
								            n13!)
							           ^ n3)
						              ^ n12)
					               ^ p11)
				             ^ p2)
			          ^ n1)
		       ^ n)
             ^ p).


D7+ : {A:o} {B:o}
          neg (! (perp (A times (perp B))))
       -o pos (perp ((? (perp B)) times (perp (? (perp A)))))
       -o #+
 = ([A:o] [B:o]
      [N^neg (! (perp (A times (perp B))))]
      [P^pos (perp ((? (perp B)) times (perp (? (perp A)))))]
      cut ^ ([p^pos cf] D5+ A B ^ N ^ p) ^ ([n^neg cf] D6+ A B ^ n ^ P)).


%query 1 1

{A:o} {B:o}
{N:neg (! (perp (A times (perp B))))}
{P:pos (perp ((? (perp B)) times (perp (? (perp A)))))}
  ce (D7+ A B ^ N ^ P) (D7 A B ^ N ^ P).